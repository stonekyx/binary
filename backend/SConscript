#!/usr/bin/python

import subprocess
import os

Import('qtEnv')

env = qtEnv.Clone()

env.Append(CPPPATH=[env.HeaderDir('elfutils/libasm.h')])

# Check necessary headers and libs
conf = Configure(env)
if not conf.CheckHeader('libelf.h') or not conf.CheckHeader('gelf.h') or not conf.CheckLib('elf'):
    print 'libelf not found!'
    Exit(1)
if not conf.CheckHeader('elfutils/libasm.h') or \
        not conf.CheckHeader('elfutils/libebl.h') or \
        not conf.CheckLib('asm') or \
        not conf.CheckLib('ebl'):
    print 'elfutils not found!'
    Exit(1)
env = conf.Finish()

env.Append(LIBS=['elf', 'asm', 'ebl'])
env.Append(RPATH=[env.LibraryDir('libasm.so')+'/elfutils'],
        LINKFLAGS=['-Wl,--enable-new-dtags']) # Generate RUNPATH

env.EnableQt4Modules([
    'QtCore'
    ])

env.Moc4('File.h')
env.Moc4('Backend.h')

libbackend = env.SharedLibrary(target='backend', source=Glob('*.cpp')+Glob('*.c')+Glob('*.cc'))
Install(env['LOCAL_LIBDIR'], libbackend)

env.TestDir('test', [libbackend])
