#!/usr/bin/python

Import('baseEnv qtEnv')

baseEnv = baseEnv.Clone()
qtEnv = qtEnv.Clone()

qtEnv.EnableQt4Modules([
    'QtGui',
    'QtCore'
    ])

def CheckPKGConfig(context, version):
     context.Message( 'Checking for pkg-config... ' )
     ret = context.TryAction('pkg-config --atleast-pkgconfig-version=%s' % version)[0]
     context.Result( ret )
     return ret

def CheckPKG(context, name):
     context.Message( 'Checking for %s... ' % name )
     ret = context.TryAction('pkg-config --exists \'%s\'' % name)[0]
     context.Result( ret )
     return ret

conf = Configure(Environment(), custom_tests = { 'CheckPKGConfig' : CheckPKGConfig,
                                               'CheckPKG' : CheckPKG })

if not conf.CheckPKGConfig('0.15.0'):
    print 'pkg-config >= 0.15.0 not found.'
    Exit(1)

if not conf.CheckPKG('libgvc >= 2.26'):
    print 'libgvc >= 2.26 not found.'
    Exit(1)

conf.Finish()

qtEnv.ParseConfig('pkg-config --cflags --libs libgvc')
qtEnv.Append(LIBS=['m'])

qtEnv.Moc4('GraphicsView.h')

pluginTarget = qtEnv.SharedLibrary(target='flow', source=Glob('*.cpp')+Glob('*.cc'))
Install(qtEnv['LOCAL_DATADIR'], pluginTarget)
