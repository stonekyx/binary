#!/usr/bin/python

Import('baseEnv qtEnv')

qtEnv = qtEnv.Clone()

conf = Configure(Environment())
if conf.CheckCHeader('libiberty/demangle.h'):
    qtEnv.Append(CPPDEFINES=['LIBIBERTY_DEMANGLE'])
elif conf.CheckCHeader('demangle.h'):
    qtEnv.Append(CPPDEFINES=['DEFAULT_DEMANGLE'])
else:
    print 'demangle.h not found!'
    Exit(1)
if conf.CheckLib('iberty_pic'):
    qtEnv.Append(LIBS=['iberty_pic'])
elif conf.CheckLib('iberty'):
    qtEnv.Append(LIBS=['iberty'])
else:
    print 'libiberty not found!'
    Exit(1)
conf.Finish()

qtEnv.EnableQt4Modules([
    'QtGui',
    'QtCore'
    ])

qtEnv.Moc4('MWBase.h')
qtEnv.Moc4('ui_MWBase.h')
qtEnv.Moc4('ui_MWTreeView.h')
qtEnv.Moc4('AutoToolTipDelegate.h')

SConscript('defines/SConscript', duplicate=1)

libTarget = qtEnv.SharedLibrary(target='plugin', source=Glob('*.cpp')+Glob('*.cc'))
Install(qtEnv['LOCAL_LIBDIR'], libTarget)

qtEnv.TestDir('test', [libTarget])
